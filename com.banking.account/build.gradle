plugins {
    id 'java'
    id 'org.springframework.boot' version '3.3.4'
    id 'io.spring.dependency-management' version '1.1.6'
}

group = 'com.msa.banking'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

repositories {
    mavenCentral()
}

ext {
    set('springCloudVersion', "2023.0.3")
    set('querydslVersion', "5.0.0") // QueryDSL 버전 추가
}

dependencies {
    // kafka
    implementation 'org.springframework.kafka:spring-kafka'
    // 유효성 검증
    implementation 'org.springframework.boot:spring-boot-starter-validation'

    // 시큐리티
    implementation 'org.springframework.boot:spring-boot-starter-security'

    // config client
    implementation 'org.springframework.cloud:spring-cloud-starter-config'

    //eureka client
    implementation 'org.springframework.cloud:spring-cloud-starter-netflix-eureka-client'

    // 모니터링
    runtimeOnly 'io.micrometer:micrometer-registry-prometheus'

    implementation 'org.springframework.cloud:spring-cloud-starter-openfeign'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    // DB
	runtimeOnly 'org.postgresql:postgresql'

    // JPA
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'

    // Jasypt Spring Boot Starter
    implementation 'com.github.ulisesbocchio:jasypt-spring-boot-starter:3.0.4'

    // Redis
	implementation 'org.springframework.boot:spring-boot-starter-data-redis'

    // QueryDSL
    implementation "com.querydsl:querydsl-jpa:${querydslVersion}:jakarta"
    annotationProcessor "com.querydsl:querydsl-apt:${querydslVersion}:jakarta"
    annotationProcessor "jakarta.annotation:jakarta.annotation-api"
    annotationProcessor "jakarta.persistence:jakarta.persistence-api"

    // mapStruct
    implementation 'org.mapstruct:mapstruct:1.5.5.Final'
    annotationProcessor 'org.mapstruct:mapstruct-processor:1.5.5.Final'

    // 해싱
    implementation 'commons-codec:commons-codec:1.15'

    // 서킷 브레이커
    implementation 'io.github.resilience4j:resilience4j-spring-boot3:2.2.0'

    // zipkin
    implementation 'io.micrometer:micrometer-tracing-bridge-brave'
    implementation 'io.zipkin.reporter2:zipkin-reporter-brave'
    implementation 'io.github.openfeign:feign-micrometer'

    // 스웨거
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.0.2'

    // cloub bus
    implementation("org.springframework.cloud:spring-cloud-starter-bus-kafka")

    implementation project(':com.banking.commonBean')

    implementation(project(":commonClass")) {
        exclude group: "org.springframework.boot", module: "spring-boot-starter-data-jpa"
    }
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

tasks.named('test') {
    useJUnitPlatform()
}

tasks.withType(JavaCompile) {
    options.compilerArgs += ['-Xlint:deprecation']
}

def querydslSrcDir = 'src/main/generated'
clean {
    delete file(querydslSrcDir)
}
